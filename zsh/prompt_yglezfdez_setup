#
# My custom prompt theme.
#
# Authors:
#   Yasser Gonzalez Fernandez <contact@yglezfdez.com>

function prompt_yglezfdez_precmd {
  # Get Git repository information.
  if (( $+functions[git-info] )); then
    git-info
  fi

  # Get Python information.
  if (( $+functions[python-info] )); then
    python-info
  fi
}

function prompt_yglezfdez_setup {
  setopt LOCAL_OPTIONS
  unsetopt XTRACE KSH_ARRAYS
  prompt_opts=(cr percent subst)

  # Load required functions.
  autoload -Uz add-zsh-hook

  # Add hook for calling {git,python}-info before each command.
  add-zsh-hook precmd prompt_yglezfdez_precmd

  # Set editor-info parameters.
  zstyle ':prezto:module:editor:info:keymap:primary' format '>>>'
  zstyle ':prezto:module:editor:info:keymap:primary:insert' format 'I'
  zstyle ':prezto:module:editor:info:keymap:primary:overwrite' format 'O'
  zstyle ':prezto:module:editor:info:keymap:alternate' format '<<<'
  zstyle ':prezto:module:editor:info:completing' format '...'

  # Set git-info parameters.
  zstyle ':prezto:module:git:info:branch' format '%Ugit%u:%b'
  zstyle ':prezto:module:git:info:keys' format 'prompt' ' %b'

  # Set python-info parameters.
  zstyle ':prezto:module:python:info:virtualenv' format ' %Uvirtualenv%u:%v'

  # Define prompts.
  PROMPT=$'\n%U%m%u:%~$git_info[prompt]$python_info[virtualenv]\n${editor_info[keymap]} '
  RPROMPT=''
}

prompt_yglezfdez_setup "$@"
